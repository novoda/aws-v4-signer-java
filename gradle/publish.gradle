apply plugin: 'com.novoda.build-properties'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

buildProperties {
    cli {
        using(project)
    }

    bintray {
        def bintrayCredentials = {
            def result = isDryRun() ?
                    ['bintrayOrg': 'n/a', 'bintrayRepo': 'n/a', 'bintrayUser': 'n/a', 'bintrayKey': 'n/a'] :
                    new File("${System.getenv('BINTRAY_PROPERTIES')}")
            println result
            return result
        }
        using(bintrayCredentials()).or(cli)
        description = '''This should contain the following properties:
                        - bintrayOrg: name of the Bintray organisation to deploy the artifacts to
                        - bintrayRepo: name of the repo of the organisation to deploy the artifacts to
                        - bintrayUser: name of the account used to deploy the artifacts
                        - bintrayKey: API key of the account used to deploy the artifacts'''
                .stripIndent()
    }
}

boolean isDryRun() {
    buildProperties.cli['dryRun'].or(true).boolean
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
}

bintray {

    user = project.buildProperties.bintray['bintrayUser'].string
    key = project.buildProperties.bintray['bintrayKey'].string
    dryRun = project.buildProperties.cli['dryRun'].or(true).boolean

    pkg {
        userOrg = project.buildProperties.bintray['bintrayOrg'].string
        repo = project.buildProperties.bintray['bintrayRepo'].string
        name = "aws-v4-signer"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/novoda/aws-v4-signer-java'
    }
}

project.publishing.publications.all { publication ->
    publication.pom.withXml {
        def root = asNode()
        root.appendNode('name', project.name)
        root.appendNode('description', 'A Kotlin-Multiplatform implementation of the AWS V4 signing algorithm for Android and iOS')
        root.appendNode('url', 'https://github.com/novoda/aws-v4-signer-java')
        root.children().last() + pomConfig
    }
}

bintrayUpload.doFirst {
    publications = project.publishing.publications
}

