apply plugin: 'java'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.novoda.build-properties'

buildProperties {
    cli {
        using(project)
    }

    bintray {
        def bintrayCredentials = {
            return isDryRun() ?
                    ['bintrayRepo': 'n/a', 'bintrayUser': 'n/a', 'bintrayKey': 'n/a'] :
                    new File("${System.getenv('BINTRAY_PROPERTIES')}")
        }
        using(bintrayCredentials()).or(cli)
        description = '''This should contain the following properties:
                         - bintrayRepo: name of the repo of the organisation to deploy the artifacts to
                         - bintrayUser: name of the account used to deploy the artifacts
                         - bintrayKey: API key of the account used to deploy the artifacts'''.stripIndent()
    }
}


version = '1.3-java7'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile project(':kmp-library')
    implementation 'com.squareup.okio:okio:2.2.2'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.2.0'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

tasks.withType(Test) {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

publish {
    userOrg = 'novoda'
    groupId = 'com.novoda'
    artifactId = 'aws-v4-signer-java'
    publishVersion = project.version
    website = 'https://github.com/novoda/aws-v4-signer-java'
    desc = 'AWS V4 Signer for Java'

    repoName = project.buildProperties.bintray['bintrayRepo'].string
    bintrayUser = project.buildProperties.bintray['bintrayUser'].string
    bintrayKey = project.buildProperties.bintray['bintrayKey'].string
}

boolean isDryRun() {
    buildProperties.cli['dryRun'].or(true).boolean
}